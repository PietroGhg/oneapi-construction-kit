; Copyright (C) Codeplay Software Limited
;
; Licensed under the Apache License, Version 2.0 (the "License") with LLVM
; Exceptions; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     https://github.com/codeplaysoftware/oneapi-construction-kit/blob/main/LICENSE.txt
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
; WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
; License for the specific language governing permissions and limitations
; under the License.
;
; SPDX-License-Identifier: Apache-2.0 WITH LLVM-exception

; RUN: %if online-spirv-as %{ spirv-as --target-env %spv_tgt_env -o %spv_file_s %s %}
; RUN: %if online-spirv-as %{ spirv-val %spv_file_s %}
; RUN: spirv-ll-tool -a OpenCL -b 64 -c GenericPointer %spv_file_s | FileCheck %s

               OpCapability Addresses
               OpCapability Linkage
               OpCapability Kernel
               OpCapability Int64
               OpCapability GenericPointer
               OpMemoryModel Physical64 OpenCL
               OpEntryPoint Kernel %20 "entry_pt"

       %void = OpTypeVoid
       %uint = OpTypeInt 32 0
      %ulong = OpTypeInt 64 0
        %_ptr_ulong = OpTypePointer CrossWorkgroup %ulong
        %_ptr_Generic_ulong = OpTypePointer Generic %ulong

        %_ptr_ptr_ulong = OpTypePointer Function %_ptr_ulong
        %_ptr_ptr_Generic_ulong = OpTypePointer Function %_ptr_Generic_ulong

    %ulong_0 = OpConstant %ulong 0
    %ulong_1 = OpConstant %ulong 1

         %x = OpConstantNull %_ptr_ulong
         %y = OpConstantNull %_ptr_Generic_ulong

         %11 = OpSpecConstantOp %_ptr_ulong GenericCastToPtr %y
         %12 = OpSpecConstantOp %_ptr_Generic_ulong PtrCastToGeneric %x

         %19 = OpTypeFunction %void
         %20 = OpFunction %void None %19
      %entry = OpLabel

; CHECK: [[GPTRPTR:%.*]] = alloca ptr addrspace(4)
 %_arg_g_ptr = OpVariable %_ptr_ptr_Generic_ulong Function

; CHECK: [[PTRPTR:%.*]] = alloca ptr addrspace(1)
   %_arg_ptr = OpVariable %_ptr_ptr_ulong Function

          %val = OpIAdd %ulong %ulong_0 %ulong_1

; CHECK: store ptr addrspace(1) addrspacecast (ptr addrspace(4) null to ptr addrspace(1)), ptr [[PTRPTR]]
               OpStore %_arg_ptr %11 Aligned 4

; CHECK: store ptr addrspace(4) addrspacecast (ptr addrspace(1) null to ptr addrspace(4)), ptr [[GPTRPTR]]
               OpStore %_arg_g_ptr %12 Aligned 4

               OpReturn
               OpFunctionEnd
