; Copyright (C) Codeplay Software Limited
;
; Licensed under the Apache License, Version 2.0 (the "License") with LLVM
; Exceptions; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     https://github.com/codeplaysoftware/oneapi-construction-kit/blob/main/LICENSE.txt
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
; WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
; License for the specific language governing permissions and limitations
; under the License.
;
; SPDX-License-Identifier: Apache-2.0 WITH LLVM-exception

; RUN: %if online-spirv-as %{ spirv-as --target-env %spv_tgt_env -o %spv_file_s %s %}
; RUN: %if online-spirv-as %{ spirv-val %spv_file_s %}
; RUN: spirv-ll-tool -a Vulkan -c Float64 %spv_file_s | FileCheck %s

               OpCapability Shader
               OpCapability Float64
          %1 = OpExtInstImport "GLSL.std.450"
               OpMemoryModel Logical GLSL450
               OpEntryPoint GLCompute %main "main"
               OpExecutionMode %main LocalSize 1 1 1
               OpSource GLSL 450
               OpMemberDecorate %inputBuffer 0 Offset 0
               ; ^ set offset of first element
               OpDecorate %inputBuffer BufferBlock

               ; v Bind variable to descriptor
               OpDecorate %inBufferVar DescriptorSet 0
               OpDecorate %inBufferVar Binding 0

               ; same for output buffer:
               OpMemberDecorate %outputBuffer 0 Offset 0
               OpDecorate %outputBuffer BufferBlock

               ; V Bind variable to descriptor
               OpDecorate %outBufferVar DescriptorSet 0
               OpDecorate %outBufferVar Binding 1

               ; Set up member offsets of ResType
               OpMemberDecorate %ResType 0 Offset 0
               OpMemberDecorate %ResType 1 Offset 8

       %void = OpTypeVoid
          %3 = OpTypeFunction %void
      %float = OpTypeFloat 32
   %floatVec = OpTypeVector %float 2
%ptrToGfvec  = OpTypePointer Uniform %floatVec
%inputBuffer = OpTypeStruct %floatVec
; ^ this is the type of the input buffer
%ptrInputBuf = OpTypePointer Uniform %inputBuffer
; declare input buffer:
%inBufferVar = OpVariable %ptrInputBuf Uniform
        %int = OpTypeInt 32 1
      %int_0 = OpConstant %int 0
    %ResType = OpTypeStruct %floatVec %floatVec
%ptrToResType = OpTypePointer Uniform %ResType
; declare type of output buffer:
%outputBuffer = OpTypeStruct %ResType
; declare output buffer:
%ptrOutBuffer = OpTypePointer Uniform %outputBuffer
%outBufferVar = OpVariable %ptrOutBuffer Uniform


       %main = OpFunction %void None %3
          %5 = OpLabel
          ; get input variable from descriptor:
 %ptrToInput = OpAccessChain %ptrToGfvec %inBufferVar %int_0
        %arg = OpLoad %floatVec %ptrToInput
     %result = OpExtInst %ResType %1 ModfStruct %arg
; store result in descriptor:
%ptrToResult = OpAccessChain %ptrToResType %outBufferVar %int_0
               OpStore %ptrToResult %result
               OpReturn
               OpFunctionEnd
; CHECK: ; ModuleID = '{{.*}}'
; CHECK: = load <2 x float>, ptr addrspace({{[0-9]}}) {{[%@].*}}
; CHECK: = alloca <2 x float>
; CHECK: = call spir_func <2 x float> @_Z4modfDv2_fPS_(<2 x float> {{[%@].*}}, ptr {{[%@].*}})
; CHECK: = insertvalue {{[%@].*}} undef, <2 x float> {{[%@].*}}, {{[0-9]+}}
; CHECK: = load <2 x float>, ptr {{[%@].*}}
; CHECK: = insertvalue {{[%@].*}} {{[%@].*}}, <2 x float> {{[%@].*}}, {{[0-9]+}}
; CHECK: store {{[%@].*}} {{[%@].*}}, ptr addrspace({{[0-9]}}) {{[%@].*}}
