; Copyright (C) Codeplay Software Limited
;
; Licensed under the Apache License, Version 2.0 (the "License") with LLVM
; Exceptions; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     https://github.com/codeplaysoftware/oneapi-construction-kit/blob/main/LICENSE.txt
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
; WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
; License for the specific language governing permissions and limitations
; under the License.
;
; SPDX-License-Identifier: Apache-2.0 WITH LLVM-exception

; RUN: %if online-spirv-as %{ spirv-as --target-env %spv_tgt_env -o %spv_file_s %s %}
; RUN: %if online-spirv-as %{ spirv-val %spv_file_s %}
; RUN: spirv-ll-tool -a OpenCL -b 64 %spv_file_s | FileCheck %s

    OpCapability Addresses
    OpCapability Kernel
    OpCapability Int8
    OpMemoryModel Physical64 OpenCL
    OpEntryPoint Kernel %20 "entry_pt"

    %void = OpTypeVoid

    %float = OpTypeFloat 32
    %uchar = OpTypeInt 8 0
    %uint = OpTypeInt 32 0


    %_ptr_uint = OpTypePointer CrossWorkgroup %uint

    %struct_user_struct = OpTypeStruct %float %uint %uchar %_ptr_uint
    %_ptr_CrossWorkgroup_struct_user_struct = OpTypePointer CrossWorkgroup %struct_user_struct

    %19 = OpTypeFunction %void %_ptr_CrossWorkgroup_struct_user_struct %uint  %uint

    %uint_0 = OpConstant %uint 0
    %uint_1 = OpConstant %uint 1
    %uint_3 = OpConstant %uint 3

    %20 = OpFunction %void None %19
    %_arg_user_struct = OpFunctionParameter %_ptr_CrossWorkgroup_struct_user_struct
    %_uint_input = OpFunctionParameter %uint
    %_uint_input2 = OpFunctionParameter %uint
   
; CHECK: define spir_kernel void @entry_pt(ptr addrspace(1) noundef [[PTR:%.*]], i32 noundef {{%.*}}, i32 noundef {{%.*}}
    %entry = OpLabel

    ; some maths for the gep input (multiple and an add)
    %mul = OpIMul %uint %uint_3 %_uint_input
; CHECK: [[MUL_RES:%.*]] = mul
    %add = OpIAdd %uint %uint_0 %_uint_input2
; CHECK: [[ADD_RES:%.*]] = add

    ; use two OpPtrAccessChain values to get to the correct index of the user struct array
    ; we don't gep into the struct
    %mul_user_struct_ptr = OpPtrAccessChain %_ptr_CrossWorkgroup_struct_user_struct %_arg_user_struct  %mul
    %add_user_struct_ptr = OpPtrAccessChain %_ptr_CrossWorkgroup_struct_user_struct %mul_user_struct_ptr %add
    %var_ptr = OpPtrAccessChain %_ptr_uint %add_user_struct_ptr %uint_0 %uint_1

; CHECK:   [[GEP_MUL:%.*]] = getelementptr %0, ptr addrspace(1) [[PTR]], i32 [[MUL_RES]]
; CHECK:   [[GEP_ADD:%.*]] = getelementptr %0, ptr addrspace(1) [[GEP_MUL]], i32 [[ADD_RES]]
; CHECK:   [[VAR_PTR:%.*]] = getelementptr %0, ptr addrspace(1) [[GEP_ADD]], i32 0, i32 1
; CHECK: store i32 [[ADD_RES]], ptr addrspace(1) [[VAR_PTR]], align 4

    OpStore %var_ptr %add Aligned 4
    OpReturn
    OpFunctionEnd
